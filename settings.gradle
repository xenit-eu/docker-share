/*
 * This settings file was generated by the Gradle 'init' task.
 *
 * The settings file is used to specify which projects to include in your build.
 * In a single project build this file can be empty or even removed.
 *
 * Detailed information about configuring a multi-project build in Gradle can be found
 * in the user guide at https://docs.gradle.org/4.0/userguide/multi_project_builds.html
 */

def includeSubprojectPerVersion(major,flavor) {
    new File("$rootProject.projectDir/2share")
            .listFiles({ File version -> version.name.startsWith(major)
            } as FileFilter)

            .collectMany ({ File versionflavor ->
                versionflavor.listFiles({ File file -> file.name.contains(flavor)
                } as FileFilter).toList()
            })

            .each { File projectFile ->
                def version = projectFile.parentFile.name
                def revision = projectFile.name
                def projectShare = "2share:${revision}"
                def projectSkeleton = "1skeleton:${version}"

                include projectSkeleton
                project(":${projectSkeleton}").projectDir = file("1skeleton/${version}")
                include projectShare
                project(":${projectShare}").projectDir = file("2share/${version}/${revision}")
            }
}

rootProject.name = 'docker-share'

if(System.getenv("VERSIONS_TO_BUILD")==null || System.getenv("VERSIONS_TO_BUILD").equals("4")) {
    includeSubprojectPerVersion("4.","community")
    if(hasProperty("enterprise")) {
        includeSubprojectPerVersion("4.","enterprise")
    }
}

if(System.getenv("VERSIONS_TO_BUILD")==null || System.getenv("VERSIONS_TO_BUILD").equals("5")) {
    includeSubprojectPerVersion("5.","community")
    if(hasProperty("enterprise")) {
        includeSubprojectPerVersion("5.","enterprise")
    }
}

if(System.getenv("VERSIONS_TO_BUILD")==null || System.getenv("VERSIONS_TO_BUILD").equals("6")) {
    includeSubprojectPerVersion("6.","community")
    if(hasProperty("enterprise")) {
        includeSubprojectPerVersion("6.","enterprise")
    }
}
